program = _{SOI ~ statement* ~ EOI}
statement = _{
    SEPARATOR
  | EmptyLine{1,}
  | schema_statement
  | property_statement
}
EmptyLine  = @{WHITESPACE* ~ NEWLINE}
/*====================================================================================================================*/
schema_statement =  {"schema" ~ SYMBOL ~ COLON ~ object}
/*====================================================================================================================*/
define_statement =  {_define ~ SYMBOL? ~ STRING_INLINE}
_define = _{"def"|"define"}
/*====================================================================================================================*/
property_statement = {_property ~ SYMBOL ~ COLON ~ object}
_property = _{"property"|"properties"|"prop"|DOT}
DOT       = @{"."}
/*====================================================================================================================*/
block = {"{" ~ (block_valid|SEPARATOR)* ~ "}"}
block_valid = _{property_statement|attitude_statement}
/*====================================================================================================================*/
attitude_statement = {key ~ SET ~ data}
key = {SYMBOL|string}
/*====================================================================================================================*/
pair = {SYMBOL ~ SET}
/*====================================================================================================================*/
COLON = @{":"}
SET = @{":"|"="}
/*====================================================================================================================*/
data = !{
    URL | Special|Cite|Byte|Number
  | string | array | object
}
/*====================================================================================================================*/
object = {SYMBOL ~ block | SYMBOL | block}
array = {"["  ~ (data|SEPARATOR)*  ~  "]"}
/*====================================================================================================================*/
///#D19A66
Special = @{"true"|"false"|"null"}
Byte    = @{"0" ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC|"_"|"-")}
/*====================================================================================================================*/
///#56B6C2
Cite = ${"$" ~ "namespace"}
/*====================================================================================================================*/
//!#D19A66: SignedNumber
Number       = ${(Exponent|SignedNumber) ~ SYMBOL?}
SignedNumber = ${Sign? ~ (Decimal|DecimalBad|Integer)}
Decimal      = ${Integer ~ DOT ~ ("_"? ~ ASCII_DIGIT)*}
DecimalBad   = ${Integer ~ DOT|DOT ~ ("_"? ~ ASCII_DIGIT)*}
//Integer    = @{"0"|ASCII_NONZERO_DIGIT ~ ("_"? ~ ASCII_DIGIT)*}
Integer      = @{ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)*}
Exponent     = ${SignedNumber ~ ("e"|"E"|"**") ~ Sign? ~ ASCII_DIGIT+}
Sign         = @{"+"|"-"}
/*====================================================================================================================*/
/// #98C379
string       = {(STRING_INLINE)}
STRING_INLINE = ${
    S1 ~ (NS|NS1)* ~ S1
  | S2 ~ (NS|NS2)* ~ S2
}
NS  = @{
    "\\u" ~ ASCII_HEX_DIGIT{4}
  | "\\u" ~ "{" ~(ASCII_HEX_DIGIT|SPACE_SEPARATOR)+ ~ "}"
  | "\\" ~ ANY
}
S1  = @{"'"}
S2  = @{"\""}
NS1 = @{!S1 ~ ANY}
NS2 = @{!S2 ~ ANY}
/*====================================================================================================================*/
///#61AFEF
SYMBOL = @{(XID_START|"_"|"$") ~ (XID_CONTINUE|"_"|"-")*}
///#3C963C
URL    = @{ASCII_ALPHA+ ~ "://" ~ (XID_CONTINUE|"_"|"-"|"/"|".")*}
/*====================================================================================================================*/
// NEWLINE = @{"\r" ~ "\n"|"\r"|"\n"}

///Gray
COMMENT          =  {MultiLineComment|LineComment}
WHITESPACE       = _{NEWLINE|SPACE_SEPARATOR|"\t"}
LineComment      = ${"//" ~ (!NEWLINE ~ ANY)*}
MultiLineComment = ${"/*" ~ (MultiLineComment | !"*/" ~ ANY)* ~ "*/"}

SEPARATOR  = _{","|";"}