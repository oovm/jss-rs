program = _{SOI ~ statement* ~ EOI}
statement = _{
    SEPARATOR
  | EmptyLine{1,}
  | DOCUMENTATION
  | schema_statement
  | property_statement
  | define_statement
}
EmptyLine  = @{WHITESPACE* ~ NEWLINE}
/*====================================================================================================================*/
schema_statement = {"schema" ~ SYMBOL ~ COLON ~ block}
/*====================================================================================================================*/
define_statement = {_define ~ SYMBOL ~ COLON ~ block}
_define = _{"def"|"define"|"^"}
/*====================================================================================================================*/
/// .key = value
property_statement = {_property ~ SYMBOL ~ COLON ~ block}
_property = _{"property"|"properties"|"prop"|DOT}
DOT       = _{"."}
/*====================================================================================================================*/
block = {key ~ object | key | object}
/*====================================================================================================================*/
/// key = value
attitude_statement = {key ~ SET ~ data}
key = {SYMBOL|STRING_INLINE}
REQUIRED = @{"!"}
/*====================================================================================================================*/
COLON = _{":"}
SET   = _{":"|"="}
/*====================================================================================================================*/
data = !{
    URL |Special|Byte|Number
  | STRING_INLINE | array | object
}
/*====================================================================================================================*/
object = {"{" ~ (object_item|SEPARATOR)* ~ "}"}
object_item = _{property_statement|attitude_statement|DOCUMENTATION}
array = {"["  ~ (data|SEPARATOR)*  ~  "]"}
/*====================================================================================================================*/
///#D19A66
Special = @{"true"|"false"|"null"}
/*====================================================================================================================*/
///#56B6C2
Cite = ${"$" ~ "namespace"}
/*====================================================================================================================*/
//!#D19A66: Number|Byte
Number       = ${Exponent|SignedNumber}
Byte         = @{"0" ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC|"_"|"-")}
SignedNumber = ${Sign? ~ (Decimal|DecimalBad|Integer)}
Decimal      = ${Integer ~ DOT ~ ("_"? ~ ASCII_DIGIT)*}
DecimalBad   = ${Integer ~ DOT|DOT ~ ("_"? ~ ASCII_DIGIT)*}
//Integer    = @{"0"|ASCII_NONZERO_DIGIT ~ ("_"? ~ ASCII_DIGIT)*}
Integer      = @{ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)*}
Exponent     = ${SignedNumber ~ ("e"|"E"|"**") ~ Sign? ~ ASCII_DIGIT+}
Sign         = @{"+"|"-"}
/*====================================================================================================================*/
/// #98C379
STRING_INLINE = ${
    S1 ~ (ESCAPED|NS1)* ~ S1
  | S2 ~ (ESCAPED|NS2)* ~ S2
}
ESCAPED  = @{
    "\\u" ~ ASCII_HEX_DIGIT{4}
  | "\\u" ~ "{" ~(ASCII_HEX_DIGIT|SPACE_SEPARATOR)+ ~ "}"
  | "\\" ~ ANY
}
S1  = _{"'"}
S2  = _{"\""}
NS1 = @{!S1 ~ ANY}
NS2 = @{!S2 ~ ANY}
/*====================================================================================================================*/
///#61AFEF
SYMBOL = @{(XID_START|"_"|"$") ~ (XID_CONTINUE|"_"|"-")*}
///#3C963C
URL    = @{ASCII_ALPHA+ ~ "://" ~ (XID_CONTINUE|"_"|"-"|"/"|".")*}
/*====================================================================================================================*/
// NEWLINE = @{"\r" ~ "\n"|"\r"|"\n"}

///Gray
COMMENT          = _{MultiLineComment|LineComment}
WHITESPACE       = _{NEWLINE|SPACE_SEPARATOR|"\t"}
DOCUMENTATION    = @{"///" ~ (!NEWLINE ~ ANY)*}
LineComment      = @{"//" ~ (!NEWLINE ~ ANY)*}
MultiLineComment = @{"/*" ~ (MultiLineComment | !"*/" ~ ANY)* ~ "*/"}

SEPARATOR  = _{","|";"}